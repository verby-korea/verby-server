buildscript {
	ext {
		springBootVersion = '2.7.1'
	}
	repositories {
		mavenCentral()
	}

	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
		classpath "io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE"
	}

}

allprojects {
	group = 'com.verby'
	version = '0.0.1'
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'jacoco'
	apply plugin: 'java-test-fixtures'

	sourceCompatibility = '17'

	repositories {
		mavenCentral()
		maven { url 'https://jitpack.io' }
	}

	dependencies {
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'
		testCompileOnly 'org.projectlombok:lombok'
		testAnnotationProcessor 'org.projectlombok:lombok'

		compileOnly 'com.github.kingbbode:spring-boot-custom-yaml-importer:0.3.0'

		testImplementation('org.springframework.boot:spring-boot-starter-test') {
			exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
		}
	}

	test {
		useJUnitPlatform()
		finalizedBy jacocoTestReport
	}

	jacocoTestReport {
		dependsOn test
		reports {
			html.enabled true
			csv.enabled false
			xml.enabled false
		}
		finalizedBy 'jacocoTestCoverageVerification'
	}

	jacocoTestCoverageVerification {
		def Qdomains = []

		for (qPattern in '*.QA'..'*.QZ') {
			Qdomains.add(qPattern + '*')
		}

		violationRules {
			rule {
				enabled = true
				element = 'CLASS'

				limit {
					counter = 'BRANCH'
					value = 'COVEREDRATIO'
					// minimum = 0.98
				}

				excludes = [] + Qdomains
			}
		}
	}

}
